buildscript {
    ext {
        springBootVersion = "1.5.15.RELEASE"
        springVersion = "4.3.4.RELEASE"
        hibernateVersion = "5.0.11.Final"
        sl4jVersion = "1.7.21"
        commonsLangVersion = "2.4"
        amazonJavaSdkVersion = "1.11.74"
        apacheCommonsIOVersion = "1.3.2"
        apacheTikaVersion = "1.14"
        fasterXmlVersion = "2.8.4"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }


}

repositories {
    mavenCentral()
}

task dependenciesGraphDot {

    mustRunAfter "clean"

    def graphBuildDir = "build/dependenciesGraph"
    def dotFile = file "$graphBuildDir/graph.dot"

    doLast {
        delete graphBuildDir
        mkdir graphBuildDir

        dotFile << "digraph dependencies {\n"

        project.subprojects.forEach { Project subProject ->
            try {

                Configuration compileConfig = subProject.configurations["compile"]

                compileConfig
                        .dependencies
                        .grep { it.respondsTo("getDependencyProject") }
                        .forEach { dotFile << """  "$subProject.name" -> "$it.dependencyProject.name"\n""" }
            } catch (Exception ignored) {
            }
        }
        dotFile << "}\n"
    }
}

task dependenciesGraph(dependsOn: "dependenciesGraphDot", type: Exec) {
    workingDir "$project.buildDir/dependenciesGraph"
    commandLine "dot", "-O", "-Tpng", "graph.dot"
}

